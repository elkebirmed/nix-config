# ============================================================================
# NixOS Configuration for Host: crazy (Personal Laptop).
# ============================================================================
{
  inputs,
  outputs,
  config,
  pkgs,
  ...
}: let
  ifTheyExist = groups: builtins.filter (group: builtins.hasAttr group config.users.groups) groups;
in {
  imports = [
    ./hardware-configuration.nix # Generated by NixOS installer

    inputs.disko.nixosModules.disko # Disk management
    inputs.home-manager.nixosModules.home-manager # Home Manager integration

    outputs.nixosModules.btrfs # Btrfs filesystem
    outputs.nixosModules.shared-config # Shared host configurations
    outputs.nixosModules.laptop # Laptop-specific configurations
    outputs.nixosModules.systemd # Systemd bootloader
    outputs.nixosModules.network-manager # NetworkManager
    outputs.nixosModules.gamemode # GameMode configurations
    outputs.nixosModules.ssh # SSH service
    outputs.nixosModules.sops # Secret management
    outputs.nixosModules.firewall # Firewall configuration
    outputs.nixosModules.podman # Containerization
    outputs.nixosModules.pipewire # Audio & Video handling
    outputs.nixosModules.bluetooth # Bluetooth support
    outputs.nixosModules.printing # Printing support
    outputs.nixosModules.fish # Fish shell
    outputs.nixosModules.sddm # SDDM display manager
    outputs.nixosModules.hyprland # Hyprland window manager
  ];

  # The name of the machine.
  networking.hostName = "crazy";

  # The time zone used when displaying times and dates.
  time.timeZone = "Africa/Algiers";

  # Determines the language for program messages,
  # the format for dates and times, sort order, and so on.
  i18n.defaultLocale = "en_GB.UTF-8";

  # Keyboard layout settings.
  # French and Arabic layouts with specific options.
  # eurosign on 'e', Caps Lock as Escape, and layout toggle with Win+Space.
  services.xserver.xkb = {
    layout = "fr,ara";
    options = "eurosign:e,caps:escape,grp:win_space_toggle";
  };

  # Make the password hash available to users.
  sops.secrets.mohamed-password-hash.neededForUsers = true;

  # Ensure the user can't modify their password or groups
  users.mutableUsers = false;

  # Main user (Mohamed).
  users.users.mohamed = {
    isNormalUser = true; # Normal user account
    shell = pkgs.fish; # Use Fish shell

    # Hashed password (from SOPS).
    hashedPasswordFile = config.sops.secrets.mohamed-password-hash.path;

    # Additional groups (If they exist) for permissions.
    extraGroups = ifTheyExist [
      "audio"
      "deluge"
      "docker"
      "git"
      "i2c"
      "libvirtd"
      "minecraft"
      "mysql"
      "network"
      "networkmanager"
      "plugdev"
      "podman"
      "tss"
      "video"
      "wheel"
      "wireshark"
    ];

    # Authorized SSH keys.
    openssh.authorizedKeys.keys = [
      # Work desktop (temporary) TODO: Remove it.
      "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIN8+qPHnSDa2UnRFj5xrN4NQvRer5a+Nnn/Q3fz4dHjN bureau-pc@DESKTOP-932LUCP"
    ];
  };

  # Use the same overlays and settings for both home-manager and NixOS.
  home-manager.useGlobalPkgs = true;

  # Pass extra arguments to home-manager modules.
  home-manager.extraSpecialArgs = {
    inherit inputs outputs;
  };

  # Import Mohamed's Home Manager configuration based on the hostname.
  home-manager.users.mohamed = import ../../home/mohamed/${config.networking.hostName}.nix;

  # Don't change this after initial setup.
  # This option defines the first version of NixOS you have installed on this
  # particular machine, and is used to maintain compatibility with
  # application data (e.g. databases) created on older NixOS versions.
  system.stateVersion = "25.05";
}
